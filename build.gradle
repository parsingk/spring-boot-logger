plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.aspectj" version "6.6.1"
}

group = 'io.github.parsingk'
archivesBaseName = "spring-boot-logger"
version = '1.1.0'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar {
    enabled = true
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication){

            groupId = 'io.github.parsingk'
            artifactId = 'spring-boot-logger'
            version = '1.1.0'
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'spring-boot-logger'
                description = 'Easy & Fast logging for JSON type on Spring boot, send Kinesis Data Stream.'
                url = 'https://github.com/parsingk/spring-boot-logger'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'parsingk'
                        name = 'xxx'
                        email = 'xxx@gmail.com'
                    }
                }
                scm {
                    connection='scm:git:git:github.com/parsingk/spring-boot-logger.git'
                    developerConnection='scm:git:https://github.com/parsingk/spring-boot-logger.git'
                    url='https://github.com/parsingk/spring-boot-logger'
                }
            }
        }
    }
    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

ext."signing.keyId" = System.getenv('SIGN_KEY_ID')
ext."signing.password" = System.getenv("SIGN_KEY_PASSWORD")
ext."signing.secretKeyRingFile" = System.getenv("SIGN_KEY_FILE")

signing {
    sign configurations.archives
    sign publishing.publications.mavenJava
}

dependencies {
    implementation group: 'org.aspectj', name: 'aspectjrt', version: '1.9.7'
    implementation group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.7'
    implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'
    implementation ('org.springframework.boot:spring-boot-starter-web:3.1.0') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'
    implementation group: 'ch.qos.logback.contrib', name: 'logback-json-classic', version: '0.1.5'
    implementation group: 'ch.qos.logback.contrib', name: 'logback-jackson', version: '0.1.5'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.0.1'
    implementation group: 'software.amazon.kinesis', name: 'amazon-kinesis-client', version: '2.4.8'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testImplementation (group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.1.0') {
        exclude group: "com.vaadin.external.google", module:"android-json"
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}
